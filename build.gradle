plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.21' apply(false)
}

apply from: 'gradle/properties.gradle'
apply from: 'gradle/wrapper.gradle'

allprojects {
    apply plugin: 'idea'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply from: "$rootDir/gradle/kotlin.gradle"

    group = 'rsmod'
    version = '0.0.4'

    repositories {
        mavenLocal()
        jcenter()
        maven { url "https://repo.maven.apache.org/maven2" }
        maven { url 'https://jitpack.io' }
    }

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.github.shyiko:ktlint:0.15.0"

        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        compile "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
        compile "io.github.microutils:kotlin-logging:$kloggingVersion"

        implementation "org.mindrot:jbcrypt:$bcryptVersion"
        implementation "com.google.code.gson:gson:$gsonVersion"
        implementation "com.github.runelite.runelite:cache:$runeliteVersion"
        implementation "io.netty:netty-all:$nettyVersion"

        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
    }

    idea {
        module {
            inheritOutputDirs = false
            outputDir = file("${project.buildDir}/classes/kotlin/main")
            testOutputDir = file("${project.buildDir}/classes/kotlin/test")
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = jvmVersion
        }
    }
}

task packageServer(type: Zip) {
    archiveFileName = "rsmod.zip"
    destinationDirectory = file('.')

    from('./') {
        include 'gradlew'
        include 'gradlew.bat'

        include 'build.gradle'
        include 'settings.gradle'

        include 'LICENSE'
        include 'README.md'

        include 'dev-settings.example.yml'
        rename 'dev-settings.example.yml', 'dev-settings.yml'

        include 'first-launch-template'
        rename 'first-launch-template', 'first-launch'
    }

    from ('gradle/') {
        into ('gradle')
    }

    from('data/') {
        into 'data'

        exclude 'cache'
        exclude 'rsa'
        exclude 'saves'
        exclude 'xteas'
    }

    from('game/') {
        into 'game'

        exclude 'build'
        exclude 'out'
        exclude 'plugins'
        exclude 'src/main/java'
        exclude 'src/test/java'
    }

    from('game/plugins') {
        into 'game/plugins'

        include 'src/main/kotlin/gg/rsmod/plugins/api/**'
        include 'src/main/kotlin/gg/rsmod/plugins/content/osrs.kts'
        include 'src/main/kotlin/gg/rsmod/plugins/service/**'
    }

    from('net/') {
        into 'net'

        exclude 'build'
        exclude 'out'
        exclude 'src/main/java'
        exclude 'src/test/java'
    }

    from('tools/') {
        into 'tools'

        exclude 'build'
        exclude 'out'
        exclude 'src/main/java'
        exclude 'src/test/java'
    }

    from('util/') {
        into 'util'

        exclude 'build'
        exclude 'out'
        exclude 'src/main/java'
        exclude 'src/test/java'
    }
}

task packageLibs(type: Zip) {
    archiveFileName = "rsmod-libs.zip"
    destinationDirectory = file('.')

    from('game/build/libs/') {
        rename "game-${project.version}.jar", "game.jar"
    }

    from('game/plugins/build/libs/') {
        rename "plugins-${project.version}.jar", "plugins.jar"
    }
}